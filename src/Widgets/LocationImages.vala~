public class Album.LocationImages : Granite.SettingsPage {
    public string folder_name { get; construct; }
    public HashTable<string, Album.ImageFlowBoxChild> table { get; set; }
    public Album.MainWindow window { get; construct; }
    public LocationImages (string folder, int index, Album.MainWindow window) {
        var home_name = Environment.get_variable ("HOME");
        var title_name = (folder == home_name) ? "Home" : Filename.display_basename (folder);
        var image = new Gtk.Image () {
            pixel_size = 32
        };

        switch (title_name) {
            case "Home":
                image.gicon = new ThemedIcon ("user-home");
                break;
            case "Documents":
                image.gicon = new ThemedIcon ("folder-documents");
                break;
            case "Music":
                image.gicon = new ThemedIcon ("folder-music");
                break;
            case "Pictures":
                image.gicon = new ThemedIcon ("folder-pictures");
                break;
            case "Videos":
                image.gicon = new ThemedIcon ("folder-videos");
                break;
            case "Downloads":
                image.gicon = new ThemedIcon ("folder-download");
                break;
            default:
                image.gicon = new ThemedIcon ("folder");
                break;
        }

        Object (
            display_widget: image,
            folder_name: folder,
            title: title_name,
            header: (index == 0) ? "Locations" : null,
            window: window
        );
    }

    construct {
        var box = new Gtk.ListBox () {
            margin_start = 20,
            margin_end = 20,
            margin_top = 20,
            margin_bottom = 20,
            selection_mode = Gtk.SelectionMode.NONE
        };
        box.set_sort_func ((row1, row2) => {
            var date1 = ((Album.SegregatedFlowbox) row1).date.split ("-");
            var date2 = ((Album.SegregatedFlowbox) row2).date.split ("-");

            var year1 = int.parse (date1[0]);
            var month1 = int.parse (date1[1]);
            var day1 = int.parse (date1[2]);

            var year2 = int.parse (date2[0]);
            var month2 = int.parse (date2[1]);
            var day2 = int.parse (date2[2]);

            if (year2 - year1 != 0) {
                return year2 - year1;
            } else {
                if (month2 - month1 != 0) {
                    return month2 - month1;
                } else {
                    return day2 - day1;
                }
            }
        });

        table = new HashTable<string, Album.ImageFlowBoxChild> (null, null);

        var file = File.new_for_path (folder_name);
        var loop = new MainLoop ();
        file.enumerate_children_async.begin ("standard::*", FileQueryInfoFlags.NOFOLLOW_SYMLINKS, Priority.DEFAULT, null, (obj, res) => {
		    try {
			    var enumerator = file.enumerate_children_async.end (res);
			    FileInfo info;
			    while ((info = enumerator.next_file (null)) != null) {
				    var content_type = info.get_content_type ();
				    if ("image" in ContentType.get_mime_type (content_type)) {
				        var filename = enumerator.get_child (info).get_path ();
				        string output;

	                    try {
		                    Process.spawn_command_line_sync ("stat '%s'".printf(filename), out output, null, null);
	                    } catch (SpawnError e) {
		                    print ("Error: %s\n", e.message);
	                    }

	                    var modification_data = output.split ("\n")[5].split (" ");
	                    var modification_date = modification_data[1];
	                    var modification_time = modification_data[2];

	                    var groupable_child = new Album.ImageFlowBoxChild (enumerator.get_child (info), modification_date, modification_time);
	                    table.insert (modification_date, groupable_child);
                    }
	            }
            } catch (Error e) {
	            print ("Error: %s\n", e.message);
            }

            loop.quit ();
        });

        loop.run ();

        string[] date_array = {};

        table.foreach ((date, widget) => {
            if (!(date in date_array)) {
                var segregated_flowbox = new Album.SegregatedFlowbox (date, window);
                segregated_flowbox.append (widget);
                date_array += date;
                box.append (segregated_flowbox);
            } else {
                for (var index = 0; index < date_array.length; index++) {
                    if (date_array[index] == date) {
                        var fb = (Album.SegregatedFlowbox) box.get_row_at_index (index);
                        fb.append (widget);
                    }
                }
            }
        });

	    var scrolled = new Gtk.ScrolledWindow () {
	        child = box,
	        hexpand = true,
	        vexpand = true
	    };

        child = scrolled;
        hexpand = true;
        vexpand = true;
    }
}

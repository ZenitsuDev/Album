public class Album.SegregatedFlowbox : Gtk.ListBoxRow {
    public Gtk.FlowBox main_widget { get; set; }
    public string date { get; construct; }
    public Album.MainWindow window { get; construct; }
    private signal void can_close (bool condition);

    private int index;

    public SegregatedFlowbox (string date, Album.MainWindow window) {
        Object (
            date: date,
            window: window
        );
    }

    construct {
        main_widget = new Gtk.FlowBox () {
            homogeneous = true,
            column_spacing = 10,
            row_spacing = 10,
            vexpand = true,
            hexpand = true,
            selection_mode = Gtk.SelectionMode.NONE,
            margin_top = 10,
            margin_bottom = 10
        };

        var container_title = new Granite.HeaderLabel (date);
        var main_container = new Gtk.Box (Gtk.Orientation.VERTICAL, 0);
        main_container.append (container_title);
        main_container.append (main_widget);
        child = main_container;
        can_focus = false;

        main_widget.child_activated.connect ((chil) => {
            var child = (Album.ImageFlowBoxChild) chil;
            index = child.get_index ();
            var paintable = ((Gtk.Image) child.child).paintable;
            window.preview_page.picture.paintable = paintable;
            window.preview_page.label.label = child.file.get_path ();

            window.transition_stack.add_shared_element (child.child, window.preview_page.picture);
            window.transition_stack.navigate (window.preview_page);
            window.preview_page.set_title (child.file.get_path ());
            window.preview_page.set_time (child.time);

            can_close (true);
        });

        can_close.connect ((condition) => {
            if (condition) {
                window.preview_page.halt_button.clicked.connect (halt_preview);
            } else {
                window.preview_page.halt_button.clicked.disconnect (halt_preview);
            }
        });
    }

    public void append (Album.ImageFlowBoxChild child) {
        main_widget.append (child);
    }

    private void halt_preview () {
        var child = (Album.ImageFlowBoxChild) main_widget.get_child_at_index (index);
        window.transition_stack.add_shared_element (window.preview_page.picture, child.child);
        window.transition_stack.navigate (window.leaflet);

        can_close (false);
    }
}
